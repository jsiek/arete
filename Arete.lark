DIGIT: /[0-9]/
INT: DIGIT+
UPPER: /[A-Z]/
LOWER: /[a-z]/
IDENT: (UPPER|LOWER) (/_/|UPPER|LOWER|DIGIT|/[₀₁₂₃₄₅₆₇₈₉!?]/)*
NEWLINE: (/\r/? /\n/)+
WS: /[ \t\f\r\n]/+
LINECOMMENT: "//" /[^\n]*/ NEWLINE
COMMENT: /\/\*([^\*]|\*+[^\/])*\*+\//

?type: "int"                                     -> int_type
    | "rational"                                 -> rational_type
    | "bool"                                     -> bool_type
    | "?"                                        -> any_type
    | "[" type "]"                               -> array_type
    | type "*"                                   -> ptr_type
    | "⟨" type_list "⟩"                          -> tuple_type
    | "(" type_list ")" "->" type                -> function_type
    | "rec" IDENT "in" type                      -> recursive_type
    | IDENT                                      -> type_var
    
?type_list: type                                 -> single
    | type "," type_list                         -> push

?type_annot: ":" type                            -> just
    |                                            -> nothing

?ret_mode: "&"                                   -> address
    |                                            -> value

?exp: exp_logic "?" exp ":" exp                  -> condition
    | "let" param "=" init "in" exp              -> let
    | exp_logic

?exp_logic: exp_logic "and" exp_cmp              -> and
    | exp_logic "or" exp_cmp                     -> or
    | exp_cmp			                  
				                  
?exp_cmp: exp_cmp "==" exp_add                   -> equal
    | exp_cmp "!=" exp_add                       -> not_equal
    | exp_cmp "<" exp_add                        -> less
    | exp_cmp ">" exp_add                        -> greater
    | exp_cmp "<=" exp_add                       -> less_equal
    | exp_cmp ">=" exp_add                       -> greater_equal
    | exp_add

?exp_add: exp_add "+" exp_med                    -> add
    | exp_add "-" exp_med                        -> sub
    | exp_med

?exp_med: exp_med "%" exp_hi                     -> int_div
    | exp_med "/" exp_hi                         -> div
    | exp_med "*" exp_hi                         -> mul
    | exp_hi

?exp_hi: INT                                     -> int
    | "copy" "(" exp ")"                         -> copy
    | "is_null" "(" exp ")"                      -> is_null
    | "null"                                     -> null
    | "len" "(" exp ")"                          -> len
    | "split" "(" exp ")"                        -> split
    | "join" "(" exp "," exp ")"                 -> join
    | "permission" "(" exp ")"                   -> permission
    | "upgrade" "(" exp ")"                      -> upgrade
    | IDENT                                      -> var
    | exp_hi "[" exp "]"                         -> index
    | "*" exp_hi                                 -> deref
    | "&" exp_hi                                 -> addrof
    | exp_hi "." IDENT                           -> member
    | "-" exp_hi                                 -> neg
    | "not" exp_hi                               -> not
    | "(" exp ")"                                -> paren
    | "fun" "(" param_list ")" ret_mode block    -> lambda
    | "new" init                                 -> new
    | "[" exp "of" exp "]"                       -> array
    | "⟨" init_list "⟩"                          -> tuple
    | exp_hi "(" init_list ")"                   -> call
    | "true"                                     -> true
    | "false"                                    -> false
    | "spawn" exp                                -> future
    | "await" exp_hi                             -> await

?stmt: exp "=" init ";"                          -> write
    | exp "<-" exp "of" exp ";"                  -> transfer
    | "!" exp ";"                                -> expr
    | "delete" exp ";"                           -> delete
    | "assert" exp ";"                           -> assert
    | "while" "(" exp ")" block                  -> while
    | block
    | if_stmt

?if_stmt: "if" "(" exp ")" block if_rest          -> if

?if_rest: "else" block                            -> else
    | "else" "if" "(" exp ")" block if_rest       -> else_if
    |                                             -> no_else

?stmt_list: "let" param "=" init ";" stmt_list   -> let_init
    | "var" IDENT "=" exp ";" stmt_list          -> var_init
    | stmt stmt_list                             -> seq
    | stmt                                       -> last_stmt
    | "return" exp ";"                           -> return

?block: "{" stmt_list "}"                        -> block
    | "{" "}"                                    -> pass

?exp_list:                                       -> empty
    | exp                                        -> single
    | exp "," exp_list                           -> push

?init: exp                                       -> default_init
    | exp "of" exp                               -> frac_init
				                 
?init_list:                                      -> empty
    | init                                       -> single
    | init "," init_list                         -> push
				                 
param: IDENT type_annot                          -> read
    | "!" IDENT type_annot                       -> write
    | "@" IDENT type_annot                       -> none
				                 
?param_list:                                     -> empty
    | param                                      -> single
    | param "," param_list                       -> push

?ident_list:                                     -> empty
    | IDENT                                      -> single
    | IDENT "," ident_list                       -> push

?ret_type: "->" type                             -> just
    |                                            -> nothing

?decl: "let" IDENT type_annot "=" exp ";"        -> global
    | "const" IDENT type_annot "=" exp ";"       -> constant
    | "fun" IDENT "(" param_list ")" ret_type ret_mode block -> function
    | "module" IDENT "exports" ident_list "{" decl_list "}" -> module
    | "from" exp "import" ident_list ";"         -> import
    | "type" IDENT "=" type ";"                  -> type_decl

?decl_list:                                      -> empty
    | decl decl_list                             -> push
    
arete: decl_list

%ignore COMMENT
%ignore LINECOMMENT
%ignore WS
