DIGIT: /[0-9]/
INT: DIGIT+
UPPER: /[A-Z]/
LOWER: /[a-z]/
IDENT: (UPPER|LOWER) (/_/|UPPER|LOWER|DIGIT|/[₀₁₂₃₄₅₆₇₈₉]/)*
NEWLINE: (/\r/? /\n/)+
WS: /[ \t\f\r\n]/+
COMMENT: "//" /[^\n]*/ NEWLINE

?exp: exp_add

?exp_add: exp "+" exp_hi         -> add
        | exp "-" exp_hi         -> sub
        | exp_hi
    
exp_hi: INT                      -> int
      | IDENT                    -> var
      | "-" exp_hi               -> neg
      | "*" exp_hi               -> deref
      | "(" exp ")"              -> paren
      | "fn" param_list "{" stmt "}" -> lambda
      | exp_hi "(" exp_list ")"  -> call
      | "new" init               -> new
      | "⟨" init_list "⟩"        -> tuple
      | exp_hi "[" exp "]"       -> index
      
?exp_list:                  -> empty
      | exp                 -> single
      | exp "," exp_list    -> push

?init: exp                        -> share_init
    | "take" exp                  -> take_init
    | "borrow" exp                -> borrow_init

?init_list:                  -> empty
      | init                 -> single
      | init "," init_list   -> push

param: IDENT               -> share
     | "take" IDENT        -> take
     | "borrow" IDENT      -> borrow

?param_list:                     -> empty
     | param                     -> single
     | param "," param_list      -> push

?basic_stmt: exp ":=" exp ";"     -> write
    | exp ";"                     -> expr
    | "{" stmt "}"                -> block

?pat: "⟨" pat_list "⟩"            -> tuple_pat
    | IDENT                       -> share_pat
    | "take" IDENT                -> take_pat
    | "borrow" IDENT              -> borrow_pat
    | "_"                         -> wildcard_pat

?pat_list:                        -> empty
    | pat                         -> single
    | pat "," pat_list            -> push

?case: "case" pat ":" stmt        -> case

?case_list: "|" case              -> single
   | "|" case case_list           -> push
   
?stmt: "var" IDENT "=" init ";" stmt   -> var_init
    | "return" exp ";"                 -> return
    | basic_stmt stmt                  -> seq
    |                                  -> pass
    | "match" exp case_list            -> match

arete: stmt

%ignore COMMENT
%ignore WS
